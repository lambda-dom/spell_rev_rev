//Divine spell patches: level 1.
//Command.
DEFINE_PATCH_FUNCTION CLERIC_COMMAND
BEGIN
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    //Change save type of first 275 opcode = third opcode.
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 174              //Play sound.
            savingthrow = 1
            savebonus = 1 - i
        END
    END
    //Use EE sleep opcode features.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 39                   //Sleep
        parameter2 = 0                      //Wake on damage.
        special = 14                        //Sleep icon and string.
    END
    //resist_dispel -> 3 in self-targeted play visual opcode.
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 215                  //Play visual.
        match_target = 1
        resist_dispel = 3
    END
END

//Cure Light Wounds.
DEFINE_PATCH_FUNCTION CLERIC_CURE_LIGHT_WOUNDS
BEGIN
    //Add remove intoxicated icon opcode.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 240                        //Remove Portrait Icon.
        target = 2
        timing = 1
        parameter2 = 5                      //Intoxicated.
        power = 1
        resist_dispel = 3
        insert_point = 4
    END
END

//Entangle.
DEFINE_PATCH_FUNCTION CLERIC_ENTANGLE
BEGIN
    //Durations 8 -> 7.
    LPF ALTER_SPELL_EFFECT INT_VAR
        duration_high = 7
    END
END

//Resist Fear.
DEFINE_PATCH_FUNCTION CLERIC_REMOVE_FEAR
BEGIN
    //Remove spurious opcode.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 206                  //Protection from spell.
    STR_VAR
        match_resource = "mummydsp"
    END
END

//Sanctuary.
DEFINE_PATCH_FUNCTION CLERIC_SANCTUARY
BEGIN
    //Duration 60 -> 36.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 174                  //Play sound.
        duration = 36
    END
END

//Shillelagh.
DEFINE_PATCH_FUNCTION CLERIC_SHILLELAGH
BEGIN
    //Change amount 0 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 111                  //Create Item.
        parameter1 = 1
    END
END

//Strength of Stone.
DEFINE_PATCH_FUNCTION CLERIC_STRENGTH_OF_STONE
BEGIN
    //Add protection against Druid version of Gust of Wind.
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_GUST_OF_WIND_DRUID_VERSION" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 206                        //Protection from spell.
        target = 1
        power = 1
        timing = 0
        duration = 300
        resistance_dispel = 3
        insert_point = 5
    STR_VAR
        resource = ~%res%~
    END
END

//Sunscorch.
DEFINE_PATCH_FUNCTION CLERIC_SUNSCORCH
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                       //Damage.
        match_savingthrow = 2                   //Save vs. breath.
    END
    //Delete Use Eff opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 177                      //Use EFF.
        match_savingthrow = 2                   //Save vs. breath.
    END
    //Add new effs.
    INNER_ACTION BEGIN
        COPY_EXISTING ~dvsuns3.eff~ ~override/dvsuns4.eff~
        COPY_EXISTING ~dvsuns3.eff~ ~override/dvsuns5.eff~
    END
    //Loop for altering.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        dicenumber = i + 1
        //Alter damage opcode.
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12                   //Damage.
            dicenumber = dicenumber
            savingthrow = 2                     //Save vs. breath.
            special = 256                       //Save for half.
        END
        //Alter resource in Use EFF.
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 177                  //Use EFF.
        STR_VAR
            resource = ~dvsuns%i%~
        END
        INNER_ACTION BEGIN
            //Alter EFF's.
            COPY_EXISTING ~dvsuns%i%.eff~ ~override~
                LPF WRITE_EFFECT_FIELD STR_VAR field = "dicenumber" value = "%dicenumber%" END
                LPF WRITE_EFFECT_FIELD STR_VAR field = "savingthrow" value = "2" END
                LPF WRITE_EFFECT_FIELD STR_VAR field = "special" value = "256" END
            BUT_ONLY
        END
    END
END

//Good berries.
DEFINE_PATCH_FUNCTION CLERIC_GOOD_BERRIES
BEGIN
    //Change power 2 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        power = 1
    END
END

//Cause Light Wounds.
DEFINE_PATCH_FUNCTION CLERIC_CAUSE_LIGHT_WOUNDS
BEGIN
    //Change power 4 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        power = 1
    END
END

//Sunscorch.
DEFINE_PATCH_FUNCTION CLERIC_SUNSCORCH
BEGIN
END

//Regenerate Light Wounds.
DEFINE_PATCH_FUNCTION CLERIC_REGENERATE_LIGHT_WOUNDS
BEGIN
    //resist_dispel 0 -> 3.
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 3
    END
    //Undo changes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321                  //Remove Effects by Resource.
        resist_dispel = 0
    END
    //Add missing display string.
    FOR (i = 1; i <= 7; ++i) BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR
            header = i
            opcode = 139                    //Display string.
            target = 2
            timing = 1
            parameter1 = 11006              //Regenerating
            power = 1
            resist_dispel = 3
            insert_point = 8
        END
    END
END

//Obscuring Mist.
DEFINE_PATCH_FUNCTION CLERIC_OBSCURING_MIST
BEGIN
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 284                  //Melee thac0 bonus.
        match_probability1 = 0
    END
    //Change resist dispel -> 2.
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 2
    END
    //Copy fixed bam.
    INNER_ACTION BEGIN
        COPY ~%MOD_FOLDER%/components/%COMPONENT%/resources/bam/dvpr121b.bam~ ~override/dvpr121b.bam~
    END
END
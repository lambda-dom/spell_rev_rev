//Includes.
//Divine spell patches: level 3.
//Animate Dead.
DEFINE_PATCH_FUNCTION CLERIC_ANIMATE_DEAD
BEGIN
    PATCH_FOR_EACH res IN ~spandead~ ~spandl12~ BEGIN
        INNER_ACTION BEGIN
            COPY_EXISTING ~%res%.eff~ ~override~
                //power 3 -> 0.
                WRITE_LONG $EFFECT_OFFSETS("power") 0
            BUT_ONLY
        END
    END
END

//Call Lightning.
DEFINE_PATCH_FUNCTION CLERIC_CALL_LIGHTNING
BEGIN
    //Change implementation to take advantage of EE.
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = 6
            //Save vs. breath + bypass MI.
            savingthrow = (1 << 1) | (1 << 24)
            savebonus = 0 - 2
            special = 256                   //Save for half.
        END
    END
END

//Dispel Magic.
DEFINE_PATCH_FUNCTION CLERIC_DISPEL_MAGIC
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 221                  //Remove Spell Type Protections.
        timing = 1
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play Visual.
        power = 0
    END
    //Move dispelling screen removal to the end.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 221              //Remove spell type protection.
    END
    INNER_ACTION BEGIN
        //Find sectype id.
        LAF FIND_FIRST_ROW STR_VAR file = "msectype" key = "k1#dispel" RET sectype = index END
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode  = 221
        target = 2
        parameter1 = 9
        parameter2 = sectype
        timing = 1
    END
END

//Glyph of Warding.
DEFINE_PATCH_FUNCTION CLERIC_GLYPH_OF_WARDING
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            savingthrow = (1 << 1) | (1 << 24)
            special = 256                   //Save for half.
        END
    END
END

//Break Enchantment.
DEFINE_PATCH_FUNCTION CLERIC_BREAK_ENCHANTMENT
BEGIN
    //Add remove portrait icons corresponding to charm.
    PATCH_FOR_EACH icon IN ~0~ ~1~ ~43~ BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 240                    //Remove portrait icon.
            target = 2
            timing = 1
            parameter2 = icon
            power = 3
            resist_dispel = 3
            insert_point = 9
        END
    END
END

//Remove Paralysis.
DEFINE_PATCH_FUNCTION CLERIC_REMOVE_PARALYSIS 
BEGIN
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 169              //Prevent icon.
    END
END

//Holy Smite.
DEFINE_PATCH_FUNCTION CLERIC_HOLY_SMITE
BEGIN
    LPF DELETE_EFFECT INT_VAR
        header = 0
        match_opcode = 177                  //Use Eff.
    END
    //Change implementation to take advantage of EE.
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. spell + bypass MI.
        match_savingthrow = 1 | (1 << 24)
    END
    //Alter damage opcode.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            //Save vs. spell + bypass MI.
            savingthrow = 1 | (1 << 24)
            special = 256                   //Save for half.
        END
    END
END

//Unholy Blight.
DEFINE_PATCH_FUNCTION CLERIC_UNHOLY_BLIGHT
BEGIN
    LPF DELETE_EFFECT INT_VAR
        header = 0
        match_opcode = 177                  //Use Eff.
    END
    //Add saves to Lighting Effects, Base Thac0 penalty, Play Sound.
    PATCH_FOR_EACH opcode IN ~141~ ~54~ ~174~ BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            match_opcode = opcode
            savingthrow = 1
        END
    END
    //Kill penalty in save's opcodes.
    PATCH_FOR_EACH opcode IN ~33~ ~34~ ~35~ ~36~ ~37~ BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            match_opcode = opcode
            savebonus = 0
        END
    END
    //Change implementation to take advantage of EE.
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. spell + bypass MI.
        match_savingthrow = 1 | (1 << 24)
    END
    //Alter damage opcode.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            //Save vs. spell + bypass MI.
            savingthrow = 1 | (1 << 24)
            special = 256                   //Save for half.
        END
    END
END

//Cure Serious Wounds.
DEFINE_PATCH_FUNCTION CLERIC_CURE_MEDIUM_WOUNDS
BEGIN
    //Add remove intoxicated icon opcode.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 240                        //Remove Portrait Icon.
        target = 2
        timing = 1
        parameter2 = 5                      //Intoxicated.
        power = 3
        resist_dispel = 3
        insert_point = 4
    END
END

//Contagion.
//Defer patching of auxiliary spell to a function to use it elsewhere.
DEFINE_PATCH_FUNCTION CLERIC_CONTAGION_AUX
BEGIN
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 78
        match_parameter2 = 10               //Slow.
    END
END

DEFINE_PATCH_FUNCTION CLERIC_CONTAGION
BEGIN
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 324              //Immunity to resource.
    END
    TEXT_SPRINT res "dvpr311d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            LPF CLERIC_CONTAGION_AUX END
        BUT_ONLY
    END
END

//Cleric's Gust of Wind.
DEFINE_PATCH_FUNCTION CLERIC_GUST_OF_WIND
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 39                   //Sleep.
        resist_dispel = 1
        special = 130
    END
END

//Summon Insects.
DEFINE_PATCH_FUNCTION CLERIC_SUMMON_INSECTS
BEGIN
    //Change implementation to take advantage of EE.
    TEXT_SPRINT res "sppr319b"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            //Delete damage opcode with a save.
            LPF DELETE_EFFECT INT_VAR
                match_opcode = 12                   //Damage.
                //Save vs. breath + bypass MI.
                match_savingthrow = (1 << 1) | (1 << 24)
            END
            //Alter damage opcode.
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 12               //Damage.
                timing = 4
                duration = 3                    //Put damage in middle of round.
                dicenumber = 2
                //Save vs. spell + bypass MI.
                savingthrow = (1 << 1) | (1 << 24)
                special = 256                   //Save for half.
            END
            //Change opcode -> panic.
            LPF ALTER_EFFECT INT_VAR
                match_opcode = 247                  //Attack nearest.
                opcode = 24                         //Panic.
                parameter2 = 0
                probability1 = 100
            END
        BUT_ONLY
    END
END

//Animal Summoning III.
DEFINE_PATCH_FUNCTION CLERIC_ANIMAL_SUMMONING_LEVEL_3
BEGIN
    TEXT_SPRINT res "wolfsu"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 8 -> 0.
            WRITE_LONG $EFFECT_OFFSETS("power") 0
        BUT_ONLY
    END
END

//Cause Serious Wounds.
DEFINE_PATCH_FUNCTION CLERIC_CAUSE_MEDIUM_WOUNDS
BEGIN
    //Change power 4 -> 3.
    LPF ALTER_SPELL_EFFECT INT_VAR
        power = 3
    END
END

//Regenerate Serious Wounds.
DEFINE_PATCH_FUNCTION CLERIC_REGENERATE_SERIOUS_WOUNDS
BEGIN
    //Change power 1 -> 3.
    LPF ALTER_SPELL_EFFECT INT_VAR
        power = 3
    END
    //Undo changes for 321 opcodes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321
        power = 0
    END
END

//Spike Growth.
DEFINE_PATCH_FUNCTION CLERIC_SPIKE_GROWTH
BEGIN
    PATCH_FOR_EACH opcode IN ~126~ ~142~ BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            match_opcode = opcode
            resist_dispel = 0
        END
    END
END

//Storm Shield.
DEFINE_PATCH_FUNCTION CLERIC_STORM_SHIELD
BEGIN
    INNER_ACTION BEGIN
        //Find sectype id.
        LAF FIND_FIRST_ROW STR_VAR file = "msectype" key = "k1insect" RET sectype = index END
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 205                    //Protection from sectype.
        target = 1
        power = 3
        parameter2 = sectype
        timing = 0
        duration = 60
        resist_dispel = 3
        insert_point = 0 - 1
    END
END
//Includes.
//Libraries.
INCLUDE ~%LIBRARY_DIR%/effects/main.tpa~
INCLUDE ~%LIBRARY_DIR%/spells/main.tpa~

//Divine spell patches: level 5.
//Animal Summoning V.
DEFINE_PATCH_FUNCTION CLERIC_ANIMAL_SUMMONING_LEVEL_5
BEGIN
    TEXT_SPRINT res "snakegsu"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 8 -> 0.
            WRITE_LONG $EFFECT_OFFSETS("power") 0
        BUT_ONLY
    END
END

//Flame Strike.
DEFINE_PATCH_FUNCTION CLERIC_FLAME_STRIKE
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 8
            savingthrow = (1 << 1) | (1 << 24)
            savebonus = 0 - 4
            special = 256                   //Save for half.
        END
    END
END

//Raise Dead.
DEFINE_PATCH_FUNCTION CLERIC_RAISE_DEAD
BEGIN
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 177              //Use Eff.
    END
END

//True Seeing.
DEFINE_PATCH_FUNCTION CLERIC_TRUE_SIGHT
BEGIN
    //Uniform resist_dispel -> 2.
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 2
    END
    //Adding missing defenses against blindness.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to portrait icon.
        target = 1
        timing = 0
        parameter2 = 8                      //Blinded.
        power = 0
        resist_dispel = 2
        duration = 60
        insert_point = 3
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 267                        //Immunity to text.
        target = 1
        timing = 5
        parameter1 = 1474                   //Blinded.
        power = 0
        resist_dispel = 2
        duration = 60
        insert_point = 4
    END
    //Protection against Cleric's obscuring mist.
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_OBSCURING_MIST" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 206                        //Immunity to spell.
        target = 1
        timing = 0
        power = 5
        resist_dispel = 2
        duration = 60
    STR_VAR
        resource = ~%res%~
    END
END

//Chaotic Commands.
DEFINE_PATCH_FUNCTION CLERIC_CHAOTIC_COMMANDS
BEGIN
    //Add missing opcodes to first header.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 267                        //Disable display string.
        target = 2
        timing = 0
        parameter1 = 14650                  //Paralyzed.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 26
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 101                        //Immunity to Effect.
        target = 2
        timing = 0
        parameter2 = 39                     //Sleep.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 34
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 101                        //Immunity to Effect.
        target = 2
        timing = 0
        parameter2 = 45                     //Stun.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 35
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to Icon.
        target = 2
        timing = 0
        parameter2 = 2                      //Rigid Thinking.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 44
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to Icon.
        target = 2
        timing = 0
        parameter2 = 14                     //Sleep.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 45
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to Icon.
        target = 2
        timing = 0
        parameter2 = 44                     //Hopelessness.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 46
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to Icon.
        target = 2
        timing = 0
        parameter2 = 55                     //Stun.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 47
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 169                        //Immunity to Icon.
        target = 2
        timing = 0
        parameter2 = 130                    //Unconscious.
        power = 5
        resist_dispel = 3
        duration = 540
        header = 1
        insert_point = 48
    END
    //Add missing protection against strings.
    READ_SHORT $SPELL_OFFSETS("headers_count") headers_count
    FOR (j = 1 ; j <= headers_count ; ++j) BEGIN
        //Held * 3, Charmed, Dire Charmed, Rigid Thinking, Feebleminded, Sleep, Stunned.
        PATCH_FOR_EACH sref IN ~340~ ~1473~ ~14102~ ~14672~ ~14780~ ~14791~ ~23744~ ~12047~ ~1280~ BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR
                opcode = 267                //Disable display string.
                target = 2
                timing = 0
                parameter1 = sref
                power = 5
                resist_dispel = 3
                duration = (8 + j) * 60
                header = j
                insert_point = 26
            END
        END
    END
END

//Greater Command.
DEFINE_PATCH_FUNCTION CLERIC_GREATER_COMMAND
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 174                  //Play sound.
        duration = 60
        savingthrow = 1
        savebonus = 0 - 4
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        power = 5
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 146                  //Cast spell.
        duration = 6                        //9 -> 6.
    STR_VAR
        resource = "sppr512a"
    END
    //Use EE sleep opcode features.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 39                   //Sleep
        special = 14                        //Sleep icon and string.
    END
END

//Mass Cure.
DEFINE_PATCH_FUNCTION CLERIC_MASS_CURE
BEGIN
    //Add remove intoxicated icon opcode.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 240                        //Remove Portrait Icon.
        target = 2
        timing = 1
        parameter2 = 5                      //Intoxicated.
        power = 5
        resist_dispel = 3
        insert_point = 3
    END
END

//Repulse Repulsion.
DEFINE_PATCH_FUNCTION CLERIC_REPULSION
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 146                  //Cast spell.
        power = 5
    END
    TEXT_SPRINT res "sppr515d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 215          //Play visual.
                power = 5
            END
    END
END

//Insect Plague.
DEFINE_PATCH_FUNCTION CLERIC_INSECT_PLAGUE
BEGIN
    //Change implementation to take advantage of EE.
    TEXT_SPRINT res "sppr517b"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            //Delete damage opcode with a save.
            LPF DELETE_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                //Save vs. breath + bypass MI.
                match_savingthrow = (1 << 1) | (1 << 24)
            END
            //Alter damage opcode.
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                timing = 4
                duration = 3                //Put damage in middle of round.
                dicenumber = 2
                //Save vs. spell + bypass MI.
                savingthrow = (1 << 1) | (1 << 24)
                special = 256               //Save for half.
            END
            //Change opcode -> panic.
            LPF ALTER_EFFECT INT_VAR
                match_opcode = 247          //Attack nearest.
                opcode = 24                 //Panic.
                parameter2 = 0
                probability1 = 100
            END
        BUT_ONLY
    END
END

//Regenerate.
DEFINE_PATCH_FUNCTION CLERIC_MASS_REGENERATE
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        power = 5                       //1 -> 5.
        resist_dispel = 3
    END
    //Undo changes for 321 opcodes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321
        power = 0
        resist_dispel = 0
    END
END

//Animal Growth.
DEFINE_PATCH_FUNCTION CLERIC_ANIMAL_GROWTH
BEGIN
    //Patch in correct resource.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 177              //Use Eff.
    STR_VAR
        resource = "dvpr525"
    END
    //Patch aux spell.
    TEXT_SPRINT res "dvpr525d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            //Strength bonus, Exceptional strength bonus and Constitution bonus.
            PATCH_FOR_EACH opcode IN ~44~ ~97~ ~10~ BEGIN
                LPF DELETE_SPELL_EFFECT INT_VAR
                    opcode_to_delete = opcode
                END
            END
    END
END

//Arcane spell patches: level 8.
//Ghostform.
DEFINE_PATCH_FUNCTION WIZARD_GHOSTFORM
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 174                  //Play sound.
        power = 8
    END
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 267                    //Prevent display string.
            target = 1
            power = 8
            parameter1 = 14662              //Poisoned
            timing = 0
            duration = (15 + i) * 6
            resist_dispel = 3
            header = i
            insert_point = 26
        END
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 267                    //Prevent display string.
            target = 1
            power = 8
            parameter1 = 17398              //Nauseated
            timing = 0
            duration = (15 + i) * 6
            resist_dispel = 3
            header = i
            insert_point = 17
        END
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 267                    //Prevent display string.
            target = 1
            power = 8
            parameter1 = 54337              //Diseased
            timing = 0
            duration = (15 + i) * 6
            resist_dispel = 3
            header = i
            insert_point = 17
        END
    END
END

//Mind Blank.
DEFINE_PATCH_FUNCTION WIZARD_MIND_BLANK
BEGIN
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        PATCH_FOR_EACH opcode IN ~2~ ~4~ BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR
                opcode = 169                //Immunity to portrait icon.
                target = 1
                timing = 0
                parameter2 = opcode
                power = 8
                resist_dispel = 3
                duration = 2400
                header = i
                insert_point = 28
            END
        END
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 101                    //Immunity to opcode.
            target = 1
            timing = 0
            parameter2 = 3
            power = 8
            resist_dispel = 3
            duration = 2400
            header = i
            insert_point = 18
        END
        PATCH_FOR_EACH opcode IN ~340~ ~1473~ ~14102~ ~14650~ ~14665~ ~14672~ ~14780~ ~23744~ BEGIN
            LPF ADD_SPELL_EFFECT INT_VAR
                opcode = 267                //Immunity to display string.
                target = 1
                timing = 0
                parameter1 = opcode
                power = 8
                resist_dispel = 3
                duration = 2400
                header = i
                insert_point = 10
            END
        END
        //Delete spurious opcodes.
        LPF DELETE_EFFECT INT_VAR
            header = i - 1
            match_opcode = 169              //Immunity to portrait icon.
            match_parameter3 = 0
        END
    END
END

//Pierce Shield.
DEFINE_PATCH_FUNCTION WIZARD_PIERCE_SHIELD
BEGIN
    TEXT_SPRINT res "spwi805b"
    INNER_ACTION BEGIN
        //power 6 -> 0.
        COPY_EXISTING ~%res%.spl~ ~override~
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 221          //Remove spell type protections.
                power = 0
            END
        BUT_ONLY
    END
END

//Monster Summoning VIII.
DEFINE_PATCH_FUNCTION WIZARD_MONSTER_SUMMONING_LEVEL_8
BEGIN
    TEXT_SPRINT res "umbhulsu"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 6 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Summon Fiend.
DEFINE_PATCH_FUNCTION WIZARD_SUMMON_FIEND
BEGIN
    TEXT_SPRINT res "spfiend"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 6 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Spell Trigger.
DEFINE_PATCH_FUNCTION WIZARD_SPELL_TRIGGER
BEGIN
    TEXT_SPRINT res "spwi809d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 258          //Activate spell sequencer.
                resist_dispel = 2
            END
        BUT_ONLY
    END
END

//Incendiary Cloud.
DEFINE_PATCH_FUNCTION WIZARD_INCENDIARY_CLOUD_AUX
BEGIN
    TEXT_SPRINT res_aux "gr#icl01"
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 324                  //Immunity to Resource.
    STR_VAR
        resource = ~%res_aux%~
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        power = 8
        dicenumber = 4
        dicesize = 6
        //Save vs. breath + bypass MI.
        savingthrow = (1 << 1) | (1 << 24)
        savebonus = 0 - 4
        special = 256
    END
END

DEFINE_PATCH_FUNCTION WIZARD_INCENDIARY_CLOUD
BEGIN
    //Delete fire damage block.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 324              //Immunity to resource.
    END
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 12               //Damage.
    END
    //resist_dispel 2 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 1
    END
    //Undo changes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 177                  //Use Eff.
        resist_dispel = 0
    END
    //Copy aux spell.
    TEXT_SPRINT res_aux "gr#icl01"
    INNER_ACTION BEGIN
        LAF COPY_SPELL INT_VAR
            name = 10002
        STR_VAR
            res = "fire_damage"
            res_name = ~%res_aux%~
            patch = "WIZARD_INCENDIARY_CLOUD_AUX"
        END
    END
    //Patch in new payload.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 146                        //Cast spell.
        target = 2
        parameter2 = 1
        timing = 1
        insert_point = 0 - 1
    STR_VAR
        resource = ~%res_aux%~
    END
END

//Symbol of Weakness.
DEFINE_PATCH_FUNCTION WIZARD_SYMBOL_WEAKNESS
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        savebonus = 0 - 4
    END
END

//Horrid Wilting.
DEFINE_PATCH_FUNCTION WIZARD_ABI_DALZIMS_HORRID_WILTING
BEGIN
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 177                  //Use Eff.
        match_parameter = 145
    END
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12           //Damage.
        //Save vs. death + bypass MI.
        match_savingthrow = (1 << 2) | (1 << 24)
    END
    //Alter damage opcode.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 15
            savingthrow = (1 << 2) | (1 << 24)
            savebonus = 0 - 2
            special = 256                   //Save for half.
        END
    END
END

//Maze.
DEFINE_PATCH_FUNCTION WIZARD_MAZE
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 2
    END
END

//Power Word Blind.
DEFINE_PATCH_FUNCTION WIZARD_POWER_WORD_BLIND
BEGIN
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_OBSCURING_MIST" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 206                        //Protection from spell.
        target = 2
        power = 8
        resist_dispel = 1
        timing = 0
        duration = 60
        insert_point = 11
    STR_VAR
        resource = ~%res%~
    END
    //Missing spell protections for permanent blindness.
    PATCH_FOR_EACH spell IN "WIZARD_OBSCURING_MIST" "CLERIC_OBSCURING_MIST" BEGIN
        LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~%spell%~ RET res = spell_res END
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 206                    //Protection from spell.
            target = 2
            power = 8
            resist_dispel = 1
            timing = 0
            duration = 3600000
            dicenumber = 5
            insert_point = 0 - 1
        STR_VAR
            resource = ~%res%~
        END
    END
END

//Symbol of Death.
DEFINE_PATCH_FUNCTION WIZARD_SYMBOL_DEATH
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 209                  //Power word kill.
        savebonus = 0 - 4
    END
END

//Bigby Icy Grasp.
DEFINE_PATCH_FUNCTION WIZARD_BIGBYS_ICY_GRASP
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 146                  //Cast spell.
        //Power 9 -> 8.
        power = 8
    END
    TEXT_SPRINT res "spwi818d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 185          //Hold II.
                resist_dispel = 1
                savebonus = 0 - 2
            END
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 174          //Play sound.
                savebonus = 0 - 2
            END
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 7            //Set color.
                resist_dispel = 1
            END
        BUT_ONLY
    END
END
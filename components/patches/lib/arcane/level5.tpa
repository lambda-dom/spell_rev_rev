//Arcane spell patches: level 5.
//Summon Shadow.
DEFINE_PATCH_FUNCTION WIZARD_SUMMON_SHADOW
BEGIN
    PATCH_FOR_EACH res IN ~shadowsu~ ~dvwraith~ BEGIN
        INNER_ACTION BEGIN
            COPY_EXISTING ~%res%.eff~ ~override~
                LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
            BUT_ONLY
        END
    END
END

//Cone of Cold.
DEFINE_PATCH_FUNCTION WIZARD_CONE_OF_COLD
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            parameter1 = i + 9
            dicenumber = i + 9
            savingthrow = (1 << 1) | (1 << 24)
            savebonus = 0 - 4
            special = 256                   //Save for half.
        END
    END
END

//Monster Summoning V.
DEFINE_PATCH_FUNCTION WIZARD_MONSTER_SUMMONING_LEVEL_5
BEGIN
    TEXT_SPRINT res "ogregrsu"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 6 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Shadow Door.
DEFINE_PATCH_FUNCTION WIZARD_SHADOW_DOOR
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        resist_dispel = 3
    END
END

//Mental domination.
DEFINE_PATCH_FUNCTION WIZARD_DOMINATION
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        savebonus = 0 - 2
    END
    //Undo changes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 324                  //Immunity to resource.
        savebonus = 0
    END
    //Add save to first 174 opcode.
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 174                  //Play sound.
        savingthrow = 1
    STR_VAR
        match_resource = "eff_m16"
    END
END

//Hold Monster.
DEFINE_PATCH_FUNCTION WIZARD_HOLD_MONSTER
BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 142                        //Display icon.
        target = 2
        timing = 0
        parameter2 = 13                     //Hold.
        power = 5
        resist_dispel = 1
        duration = 60
        savingthrow = 1
        savebonus = 0 - 2
        insert_point = 2
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 139                        //Display string.
        target = 2
        timing = 1
        parameter1 = 14102                  //Held.
        power = 5
        resist_dispel = 1
        savingthrow = 1
        savebonus = 0 - 2
        insert_point = 2
    END
END

//Dispelling screen.
DEFINE_PATCH_FUNCTION WIZARD_DISPELLING_SCREEN
BEGIN
    TEXT_SPRINT res_aux "k1#scre"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res_aux%.spl~ ~override~
            TEXT_SPRINT res "spdispma"
            LPF ADD_SPELL_EFFECT INT_VAR
                opcode = 296                        //Protection from animation.
                target = 2
                power = 5
                timing = 0
                resist_dispel = 2
                duration = 300
                insert_point = 0 - 1
            STR_VAR
                resource = ~%res%~
            END
        BUT_ONLY
    END
END

//Breach.
DEFINE_PATCH_FUNCTION WIZARD_BREACH
BEGIN
END

//Conjure Lesser Fire Elemental.
DEFINE_PATCH_FUNCTION WIZARD_CONJURE_LESSER_FIRE_ELEMENTAL
BEGIN
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        probability1 = 85
        probability2 = 0
    STR_VAR
        match_resource = "spportal"
    END
    TEXT_SPRINT res "splesfir"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 5 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Phantom Blade.
DEFINE_PATCH_FUNCTION WIZARD_PHANTOM_BLADE
BEGIN
    //Change amount 0 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 111                  //Create Item.
        parameter1 = 1                      //0 -> 1.
    END
END

//Conjure Lesser Air Elemental.
DEFINE_PATCH_FUNCTION WIZARD_CONJURE_LESSER_AIR_ELEMENTAL
BEGIN
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        probability1 = 85
        probability2 = 0
    STR_VAR
        match_resource = "spportal"
    END
    TEXT_SPRINT res "splesair"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 5 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Conjure Lesser Earth Elemental.
DEFINE_PATCH_FUNCTION WIZARD_CONJURE_LESSER_EARTH_ELEMENTAL
BEGIN
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        probability1 = 85
        probability2 = 0
    STR_VAR
        match_resource = "spportal"
    END
    TEXT_SPRINT res "splesear"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 5 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Spell Deflection.
DEFINE_PATCH_FUNCTION WIZARD_SPELL_DEFLECTION
BEGIN
    LPF DELETE_EFFECT INT_VAR
        multi_match = 1
        match_opcode = 233                  //Modify proficiencies.
    END
END

//Fire Burst.
DEFINE_PATCH_FUNCTION WIZARD_SUN_FIRE
BEGIN
    //Remove spurious double immunity opcode.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 233              //Immunity to resource.
    END
    //Use EE damage opcode features on aux spell.
    TEXT_SPRINT res "spwi523d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            //Delete damage opcode with a save.
            LPF DELETE_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                //Save vs. breath + bypass MI.
                match_savingthrow = (1 << 1) | (1 << 24)
            END
            //Alter damage opcode.
            LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
            FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
                LPF ALTER_SPELL_EFFECT INT_VAR
                    header = i
                    match_opcode = 12       //Damage.
                    dicenumber = i + 9
                    savingthrow = (1 << 1) | (1 << 24)
                    savebonus = 0 - 4
                    special = 256           //Save for half.
                END
            END
        BUT_ONLY
    END
END

//Mestil's Acid Sheath.
DEFINE_PATCH_FUNCTION WIZARD_MESTILS_ACID_SHEATH
BEGIN
    INNER_ACTION BEGIN
        //Find sectype id.
        LAF FIND_FIRST_ROW STR_VAR file = "msectype" key = "k1insect" RET sectype = index END
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 205                        //Protection from sectype.
        target = 1
        power = 5
        parameter2 = sectype
        timing = 0
        duration = 120
        resist_dispel = 3
        insert_point = 0 - 1
    END
    PATCH_FOR_EACH spell_name IN "CLERIC_SUMMON_INSECTS" "CLERIC_INSECT_PLAGUE" "CLERIC_CREEPING_DOOM" BEGIN
        LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~%spell_name%~ RET res = spell_res END
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 206                    //Protection from spell.
            target = 1
            power = 5
            timing = 0
            duration = 120
            resist_dispel = 3
            insert_point = 0 - 1
        STR_VAR
            resource = ~%res%~
        END
    END
END

//Waves of fatigue.
DEFINE_PATCH_FUNCTION WIZARD_WAVES_OF_FATIGUE
BEGIN
    INNER_ACTION BEGIN
        //Fixed bam.
        COPY ~%MOD_FOLDER%/components/%COMPONENT%/resources/bam/spwi508b.bam~ ~override/spwi508b.bam~
    END
END
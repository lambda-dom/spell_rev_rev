//Includes.
//Libraries.
INCLUDE ~%LIBRARY_DIR%/tables/main.tpa~
INCLUDE ~%LIBRARY_DIR%/spells/main.tpa~
INCLUDE ~%LIBRARY_DIR%/effects/main.tpa~

//Arcane spell patches: level 3.
//Clairvoyance.
DEFINE_PATCH_FUNCTION WIZARD_CLAIRVOYANCE
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 36                   //Save vs. breath.
        power = 2
    END
    //Cleaning.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321
        target = 1
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 141
        target = 1
    END
END

//Dispel Magic.
DEFINE_PATCH_FUNCTION WIZARD_REMOVE_MAGIC
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 221                  //Remove Spell Type Protections.
        timing = 1
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play Visual.
        power = 0
    END
    //Move dispelling screen removal to the end.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 221              //Remove spell type protection.
    END
    INNER_ACTION BEGIN
        //Find sectype id.
        LAF FIND_FIRST_ROW STR_VAR file = "msectype" key = "k1#dispel" RET sectype = index END
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode  = 221
        target = 2
        parameter1 = 9
        parameter2 = sectype
        timing = 1
    END
END

//Flame Arrow.
DEFINE_PATCH_FUNCTION WIZARD_FLAME_ARROW_AUX
BEGIN
    TEXT_SPRINT res_aux "gr#fla01"
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 324                  //Immunity to Resource.
    STR_VAR
        resource = ~%res_aux%~
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        power = 3
        dicenumber = 1
        dicesize = 6
    END
END

DEFINE_PATCH_FUNCTION WIZARD_FLAME_ARROW
BEGIN
    //Delete 324 opcodes blocking spell on fire immunity.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 324
    END
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_FLAME_ARROW" RET res = spell_res END
    PATCH_FOR_EACH res_aux IN ~%res%a~ ~%res%b~ ~%res%c~ ~%res%d~ ~%res%e~ BEGIN
        INNER_ACTION BEGIN
            COPY_EXISTING ~%res_aux%.spl~ ~override~
                LPF DELETE_SPELL_EFFECT INT_VAR
                    opcode_to_delete = 324
                END
            BUT_ONLY
        END
    END
    //Reinstate fire immunity protection.
    //Copy fire damage aux spell.
    TEXT_SPRINT res_aux "gr#fla01"
    INNER_ACTION BEGIN
        LAF COPY_SPELL INT_VAR
            name = 10003
        STR_VAR
            res = "fire_damage"
            res_name = ~%res_aux%~
            patch = "WIZARD_FLAME_ARROW_AUX"
        END
    END
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%e.spl~ ~override~
            //Delete fire damage opcode.
            LPF DELETE_EFFECT INT_VAR
                match_opcode = 12                   //Damage.
                match_parameter2 = 0x80000          //Normal fire damage type.
            END
            //Patch in new payload.
            LPF ADD_SPELL_EFFECT INT_VAR
                opcode = 146                        //Cast spell.
                target = 2
                parameter2 = 1
                timing = 1
                insert_point = 0 - 1
            STR_VAR
                resource = ~%res_aux%~
            END
        BUT_ONLY
    END
END

//Fireball.
DEFINE_PATCH_FUNCTION WIZARD_FIREBALL
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            savingthrow = (1 << 1) | (1 << 24)
            savebonus = 0 - 2
            special = 256                   //Save for half.
        END
    END
END

//Hold Person.
DEFINE_PATCH_FUNCTION WIZARD_HOLD_PERSON
BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 139                        //Display string.
        target = 2
        power = 3
        timing = 1
        parameter1 = 14102                  //Held.
        resist_dispel = 1
        savingthrow = 1
        savebonus = 0 - 2
        insert_point = 2
    END
END

//Lightning Bolt.
DEFINE_PATCH_FUNCTION WIZARD_LIGHTNING_BOLT
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        match_savingthrow = 2               //Save vs. breath.
    END
    //Alter damage opcode.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            savingthrow = 2                 //Save vs. breath.
            savebonus = 0 - 4
            special = 256                   //Save for half.
        END
    END
END

//Monster Summoning III.
DEFINE_PATCH_FUNCTION WIZARD_MONSTER_SUMMONING_LEVEL_3
BEGIN
    TEXT_SPRINT res "hobarcsu"
    INNER_ACTION BEGIN
        //power 6 -> 0.
        COPY_EXISTING ~%res%.eff~ ~override~
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Non-detection.
DEFINE_PATCH_FUNCTION WIZARD_NON_DETECTION
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 206                  //Protection from Spell.
        power = 3
    END
END

//Slow.
DEFINE_PATCH_FUNCTION WIZARD_SLOW
BEGIN
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 221                  //Remove Spell Type Protections.
        match_savebonus = 0 - 2                   
        savebonus = 0
    END
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 174                  //Play Sound.
        timing = 1
        savingthrow = 16                    //save vs. polymorph
        savebonus = 0 - 2
    STR_VAR
        match_resource = "eff_m29"
    END
END

//Skull Trap.
DEFINE_PATCH_FUNCTION WIZARD_SKULL_TRAP
BEGIN
    //Delete damage opcode with a save.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        //Save vs. breath + bypass MI.
        match_savingthrow = (1 << 1) | (1 << 24)
    END
    //Alter damage opcode.
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1 ; i <= headers_count ; ++i) BEGIN
        LPF ALTER_SPELL_EFFECT INT_VAR
            header = i
            match_opcode = 12               //Damage.
            dicenumber = i + 4
            savingthrow = (1 << 1) | (1 << 24)
            savebonus = 0 - 2
            special = 256                   //Save for half.
        END
    END
END

DEFINE_PATCH_FUNCTION WIZARD_VAMPIRIC_TOUCH_AUX
BEGIN
    TEXT_SPRINT res_aux "gr#vmt01"
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 318                        //Immunity to Resource
        target = 2
        parameter2 = 43                     //Source.
        insert_point = 0
    STR_VAR
        resource = ~%res_aux%~
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 146                        //Cast spell.
        target = 2
        parameter2 = 0                      //At caster level.
        timing = 1
        resist_ = 1
    STR_VAR
        resource = ~%res_aux%~
    END
END

DEFINE_PATCH_FUNCTION WIZARD_VAMPIRIC_TOUCH
BEGIN
    //Delete all spell headers but first.
    //DELETE_SPELL_HEADER is currently bugged so we roll out our own deletion loop.
    DEFINE_ASSOCIATIVE_ARRAY indices BEGIN END
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    //Gather spell header indices to delete.
    FOR (i = headers_count - 1; i >= 0; --i) BEGIN
        LPF GET_SPELL_HEADER_OFFSET INT_VAR header = i RET header_offset = offset END
        READ_SHORT (header_offset + $SPELL_HEADER_OFFSETS("min_level")) level
        PATCH_IF (level >= 2) BEGIN
            TEXT_SPRINT $indices("%i%") "*"
        END
    END
    //Delete spell headers.
    PHP_EACH indices AS index => dummy BEGIN
        LPF REMOVE_SPELL_HEADER INT_VAR header = index END
    END
    //Delete all opcodes except 177: damage, max hp bonus, lighting effects, play sound, play visual effect, protection from spell, immunity from resource.
    PATCH_FOR_EACH opcode IN ~12~ ~18~ ~141~ ~174~ ~215~ ~206~ ~324~ BEGIN
        LPF DELETE_SPELL_EFFECT INT_VAR
            opcode_to_delete = opcode
        END
    END
    //Patch in new payload.
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_VAMPIRIC_TOUCH" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 318                        //Immunity to Resource
        target = 2
        parameter2 = 154                    //Immunity to Magic Damage.
        insert_point = 0
    STR_VAR
        resource = ~%res%~
    END
    //Copy aux spell, essentially a copy of vanilla aux spell.
    TEXT_SPRINT res_aux "gr#vmt01"
    INNER_ACTION BEGIN
        LAF COPY_SPELL INT_VAR
            name = 10001
        STR_VAR
            res = "vampiric_touch"
            res_name = ~%res_aux%~
            patch = "WIZARD_VAMPIRIC_TOUCH_AUX"
        END
    END
END

//Wraithform.
DEFINE_PATCH_FUNCTION WIZARD_WRAITH_FORM
BEGIN
    //resist_dispel -> 3.
    LPF ALTER_SPELL_EFFECT INT_VAR
        resist_dispel = 3
    END
    //Undo changes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321                  //Remove Effects by Resource.
        resist_dispel = 0
    END
    //Delete spurious innate blocking opcodes.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 145                  //Disable spellcasting.
        match_parameter2 = 2                //Innate.
    END
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 144                  //Disable Button.
        match_parameter2 = 13               //Special abilities.
    END
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 139                  //Display string.
        power = 3
    END
END

//Dire Charm.
DEFINE_PATCH_FUNCTION WIZARD_DIRE_CHARM
BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 139                        //Display string.
        target = 2
        power = 3
        parameter1 = 14780                  //"Dire Charmed"
        timing = 1
        resist_dispel = 1
        savingthrow = 1
        insert_point = 17
    END
    //Simplify implementation of racial immunities.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 177              //Use Eff.
    END
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_CHARM_PERSON" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 318                        //Immunity to resource.
        target = 2
        parameter2 = 15                     //Elf.
        probability1 = 90
        insert_point = 1
    STR_VAR
        resource = ~%res%~
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 318                        //Immunity to resource.
        target = 2
        parameter2 = 19                     //Half-Elf.
        probability1 = 30
        insert_point = 1
    STR_VAR
        resource = ~%res%~
    END
END

//Minor Spell Deflection.
DEFINE_PATCH_FUNCTION WIZARD_MINOR_SPELL_DEFLECTION
BEGIN
    //Delete spurious duplicate opcodes.
    LPF DELETE_EFFECT INT_VAR
        multi_match = 1
        match_opcode = 328                  //Set Spell State
    END
    LPF DELETE_EFFECT INT_VAR
        multi_match = 1
        match_opcode = 233                  //Modify Proficiencies
    END
END

//Spell Thrust.
DEFINE_PATCH_FUNCTION WIZARD_SPELL_THRUST
BEGIN
    TEXT_SPRINT res "dvsplthr"
    INNER_ACTION BEGIN
        //Change area radius of projectile -> 15'.
        COPY_EXISTING ~%res%.pro~ ~override~
            WRITE_SHORT 0x204 150
        BUT_ONLY
    END
END


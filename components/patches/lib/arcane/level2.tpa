//Arcane spell patches: level 2.
//Blur.
DEFINE_PATCH_FUNCTION WIZARD_BLUR
BEGIN
    //Power of opcodes 3 -> 2.
    LPF ALTER_SPELL_EFFECT INT_VAR 
        power = 2
    END
    //Undo changes.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 321                  //Remove Effects by Resource.
        power = 0
    END
END

//Detect Invisibility.
DEFINE_PATCH_FUNCTION WIZARD_DETECT_INVISIBILITY
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 193                  //Invisibility Detection.
        resist_dispel = 3
    END
END

//Horror.
DEFINE_PATCH_FUNCTION WIZARD_HORROR
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play visual effect.
        resist_dispel = 1
    END
END

//Battering Ram.
DEFINE_PATCH_FUNCTION WIZARD_BATTERING_RAM
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 39                   //Sleep
        special = 130                       //Unconscious.
    END
END

//Know opponent.
DEFINE_PATCH_FUNCTION WIZARD_KNOW_OPPONENT
BEGIN
    LPF ALTER_SPELL_HEADER INT_VAR 
        speed = 2                           //Casting speed 9 -> 2.
    END
    //Change immunities implementation.
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 177              //Use EFF.
    END
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_KNOW_OPPONENT" RET res = spell_res END
    PATCH_FOR_EACH race IN ~GOLEM~ BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR
            opcode = 318                    //Immunity to resource.
            target = 2
            parameter2 = 27                 //Race = Golem
            insert_point = 0
        STR_VAR
            resource = ~%res%~
        END
    END
    //Non-stacking as in Cleric's version.
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 321                        //Remove effects by resource.
        target = 2
        insert_point = 0
    STR_VAR
        resource = ~%res%~
    END
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_KNOW_OPPONENT" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 321                        //Remove effects by resource.
        target = 2
        insert_point = 0
    STR_VAR
        resource = ~%res%~
    END
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 206              //Protection from spell.
    END
END

//Luck.
DEFINE_PATCH_FUNCTION WIZARD_LUCK
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 276                  //Detect Illusion Bonus
        resist_dispel = 3
    END
END

//Resist Fear.
DEFINE_PATCH_FUNCTION WIZARD_RESIST_FEAR
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 23                   //Reset morale.
        duration = 0
    END
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 206                  //Protection form spell.
    STR_VAR
        match_resource = "mummydsp"
    END
END


DEFINE_PATCH_FUNCTION WIZARD_MELF_ACID_ARROW
BEGIN
    LPF DELETE_SPELL_EFFECT INT_VAR
        opcode_to_delete = 324              //Immunity to resource.
    END
    //Reinstate acid immunity.
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_MELF_ACID_ARROW" RET res = spell_res END
    LPF GET_SPELL_HEADER_COUNT RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        LPF ADD_SPELL_EFFECT INT_VAR
            header = i
            opcode = 324                    //Immunity to resource.
            target = 2
            parameter2 = 97                 //Not acid immune.
            insert_point = 1                //After missile damage.
        STR_VAR
            resource = ~%res%~
        END
    END
END

//Mirror Image.
DEFINE_PATCH_FUNCTION WIZARD_MIRROR_IMAGE
BEGIN
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "WIZARD_MIRROR_IMAGE" RET res = spell_res END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 321                        //Remove effect by resource.
        target = 1
        timing = 0
        insert_point = 0
    STR_VAR
        resource = ~%res%~
    END
END

//Monster Summoning II.
DEFINE_PATCH_FUNCTION WIZARD_MONSTER_SUMMONING_LEVEL_2
BEGIN
    TEXT_SPRINT res "jellgrsu"
    INNER_ACTION BEGIN
        //power 6 -> 0.
        COPY_EXISTING ~%res%.eff~ ~override~
            //power 6 -> 0.
            LPF WRITE_EFFECT_FIELD STR_VAR field = "power" value = "0" END
        BUT_ONLY
    END
END

//Ghoul Touch.
DEFINE_PATCH_FUNCTION WIZARD_GHOUL_TOUCH
BEGIN
    //Change amount 0 -> 1.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 111                  //Create Item.
        parameter1 = 1
    END
END

//Vocalize.
DEFINE_PATCH_FUNCTION WIZARD_VOCALIZE
BEGIN
    //Change power 1 -> 2.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 215                  //Play Visual Effect.
        power = 2
    END
END

//Power Word Sleep.
DEFINE_PATCH_FUNCTION WIZARD_POWER_WORD_SLEEP
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 39                   //Sleep
        special = 14                        //Sleep icon and string.
    END
END

//Ray of Enfeeblement.
DEFINE_PATCH_FUNCTION WIZARD_RAY_OF_ENFEEBLEMENT
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 50                   //Character pulse.
        savingthrow = 1
        savebonus = 0 - 2
    END
    //Alter first play sound opcode.
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 174                  //Play sound.
        match_timing = 1
        savingthrow = 1
        savebonus = 0 - 2
    END
END

//Chaos Shield.
DEFINE_PATCH_FUNCTION WIZARD_CHAOS_SHIELD
BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
        target = 1
    END
END

//Sound Burst.
DEFINE_PATCH_FUNCTION WIZARD_SOUND_BURST
BEGIN
    TEXT_SPRINT res "spwi223d"
    INNER_ACTION BEGIN
        COPY_EXISTING ~%res%.spl~ ~override~
            //Delete damage opcode with a save.
            LPF DELETE_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                match_savingthrow = 2       //Save vs. breath.
            END
            //Alter damage opcode.
            LPF ALTER_SPELL_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                power = 2
                timing = 1
                resist_dispel = 1
                dicenumber = 4
                savingthrow = 2             //Save vs. breath.
                savebonus = 0 - 2
                special = 256               //Save for half.
            END
        BUT_ONLY
    END
END

//Glitterdust.
DEFINE_PATCH_FUNCTION WIZARD_GLITTERDUST
BEGIN
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 206                  //Protection from spell.
    STR_VAR
        match_resource = "sppr114f"
    END
END